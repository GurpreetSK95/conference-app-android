import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.novoda.static-analysis'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.mentalmachines.droidcon_boston"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 17
        versionName "2.0.9"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        if (rootProject.file('upload.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('upload.properties')))
            release {
                storeFile rootProject.file(props.keyStore)
                storePassword props.keyStorePassword
                keyAlias props.keyAlias
                keyPassword props.keyAliasPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (rootProject.file('upload.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

staticAnalysis {
    penalty {
        maxErrors = 150 // current threshold value
        maxWarnings = 70 // current threshold value
    }

    lintOptions {
        lintConfig rootProject.file('team-props/lint-config.xml')
        checkReleaseBuilds false
        warningsAsErrors true
    }

    detekt {
        config = rootProject.files('team-props/detekt-config.yml')
        filters = '.*test.*,.*/resources/.*,.*/tmp/.*'
    }

    ktlint {
        android true
        reporters = [ReporterType.CHECKSTYLE]

        includeVariants { variant -> variant.name.contains('debug') }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //region Version
    final junit = "4.12"
    final mockito = "2.21.0"
    final support = '1.0.0'
    final appCompat = '1.0.2'
    final firebaseDatabase = '16.0.5'
    final firebaseCore = '16.0.6'
    final firebaseMessaging = '17.3.4'
    final crashlytics = '2.9.1'
    final gson = '2.8.2'
    final flexibleAdapter = '5.0.1'
    final flexibleAdapterUi = '1.0.0-b3'
    final glide = '3.7.0'
    final threeTenAbp = '1.0.5'
    final recyclerRefreshLayout = '2.0.5'
    //endregion

    //region Dependencies
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // TESTING
    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"

    // Support
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation "androidx.legacy:legacy-support-v4:$support"
    implementation "androidx.cardview:cardview:$support"
    implementation "com.google.android.material:material:$support"
    implementation "androidx.browser:browser:$support"

    // Firebase
    implementation "com.google.firebase:firebase-messaging:$firebaseMessaging"
    implementation "com.google.firebase:firebase-database:$firebaseDatabase"
    implementation "com.google.firebase:firebase-core:$firebaseCore"

    implementation 'com.firebaseui:firebase-ui-auth:4.3.1'
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0'

    // Crashlytics only for release builds
    releaseImplementation("com.crashlytics.sdk.android:crashlytics:$crashlytics@aar") {
        transitive = true
    }

    // Misc
    implementation "com.google.code.gson:gson:$gson"
    implementation "eu.davidea:flexible-adapter:$flexibleAdapter"
    implementation "eu.davidea:flexible-adapter-ui:$flexibleAdapterUi"
    implementation "com.github.bumptech.glide:glide:$glide"
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenAbp"
    implementation "com.dinuscxj:recyclerrefreshlayout:$recyclerRefreshLayout"
    //endregion
}

apply plugin: 'com.google.gms.google-services'